apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: "androidx.navigation.safeargs.kotlin"


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.awscherb.cardkeeper"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        versionCode 56
        versionName "1.0"
    }

    flavorDimensions "env"

    productFlavors {
        dev {
            dimension "env"
            applicationId "com.awscherb.cardkeeper.dev"
            buildConfigField "Boolean", "PRODUCTION", "false"
        }
        prod {
            dimension "env"
            applicationId "com.awscherb.cardkeeper"
            buildConfigField "Boolean", "PRODUCTION", "true"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    sourceSets {
        dev {
            java.srcDirs += 'src/prod'
        }
    }

}

dependencies {

    // RX
    def rxJavaVersion = '2.2.9'
    def rxAndroidVersion = '2.1.1'
    def rxBindingVersion = '3.0.0-alpha2'
    def rxLifecycleVersion = '3.0.0'

    def navVersion = "2.1.0-alpha05"
    def roomVersion = "2.1.0"

    // App dependencies
    def daggerVersion = '2.16'
    def junitVersion = '4.12'
    def mockitoVersion = '2.28.2'
    def hamcrestVersion = '1.3'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation "junit:junit:$junitVersion"

    // Support libs
    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha06'
    implementation 'com.google.android.material:material:1.1.0-alpha07'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    kapt 'androidx.lifecycle:lifecycle-compiler:2.2.0-alpha01'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    implementation "com.jakewharton.rxbinding3:rxbinding:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-core:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-material:$rxBindingVersion"

    implementation "com.trello.rxlifecycle3:rxlifecycle:$rxLifecycleVersion"
    implementation "com.trello.rxlifecycle3:rxlifecycle-components:$rxLifecycleVersion"

    // Scanning
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0@aar'

    // Dependency Injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // Fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true;
    }

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    debugImplementation 'androidx.fragment:fragment-testing:1.1.0-beta01'

}

kapt {
    generateStubs = true
}

configurations.all {
    resolutionStrategy.force "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.ext.kotlinVersion"
}

androidExtensions {
    experimental = true
}