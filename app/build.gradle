apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"


android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.awscherb.cardkeeper"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        versionCode 92
        versionName "1.1"
    }

    flavorDimensions "env"

    productFlavors {
        dev {
            dimension "env"
            applicationId "com.awscherb.cardkeeper.dev"
            buildConfigField "Boolean", "PRODUCTION", "false"
        }
        prod {
            dimension "env"
            applicationId "com.awscherb.cardkeeper"
            buildConfigField "Boolean", "PRODUCTION", "true"
        }
    }

    buildTypes {
        Properties properties = new Properties()
        properties.load(project.file('clrvynce.properties').newDataInputStream())
        def cvyKey = properties.getProperty('API_KEY')
        release {
            resValue "string", "clrvynceKey", cvyKey
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            resValue "string", "clrvynceKey", cvyKey
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    sourceSets {
        dev {
            java.srcDirs += 'src/prod'
        }
    }

}

dependencies {

    // RX
    def rxJavaVersion = '2.2.19'
    def rxAndroidVersion = '2.1.1'

    // App dependencies
    def daggerVersion = '2.27'
    def junitVersion = '4.12'
    def mockitoVersion = '2.28.2'
    def hamcrestVersion = '1.3'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation "junit:junit:$junitVersion"

    // Support libs
    implementation 'androidx.appcompat:appcompat:1.2.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha02'
    implementation 'com.google.android.material:material:1.2.0-alpha06'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.0-alpha05"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.0-alpha05"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.0-alpha01"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0-alpha01"

    // Room
    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-ktx:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"
    kapt 'androidx.lifecycle:lifecycle-common-java8:2.3.0-alpha01'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.72"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    implementation "com.jakewharton.rxbinding3:rxbinding:3.1.0"
    implementation "com.jakewharton.rxbinding3:rxbinding-core:3.1.0"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:3.1.0"
    implementation "com.jakewharton.rxbinding3:rxbinding-material:3.1.0"

    implementation "com.trello.rxlifecycle3:rxlifecycle:3.1.0"
    implementation "com.trello.rxlifecycle3:rxlifecycle-components:3.1.0"

    // Scanning
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0@aar'

    // Dependency Injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    implementation 'com.clrvynce.android:sdk:1.0.1'

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    debugImplementation 'androidx.fragment:fragment-testing:1.3.0-alpha03'

}